top = _{ SOI ~ (top_structs ~ ",")* ~ top_structs? ~ EOI }
top_structs = _{ props | tags | text | emphasis | code | paragraph}

props = { "props" ~ "{" ~ props_inner ~ "}" }
props_inner = _{ (prop_tuple ~ ",")* ~ prop_tuple? }
prop_tuple = { ("(" ~ string ~ "," ~ prop_val ~ ")") }
prop_val = _{ string | text | date | int }

tags = { "tags" ~ "{" ~ strings ~ "}" }

paragraph = { "par" ~ "{" ~ (par_item ~ ",")* ~ par_item? ~ "}" }
par_item = _{ text | emphasis | code | props | tags }

emphasis = { "em" ~ "{" ~ em_st ~ "," ~ text ~ "}" }
em_st = { "le" | "me" | "se" | "ld" | "md" | "sd" }

code = { "code" ~ "{" ~ string ~ "," ~ string ~ "," ~ code_text ~ ("," ~ props)? ~ ("," ~ tags)? ~ "}" }
code_text = ${ code_txt ~ code_text_inner ~ code_txt }
code_text_inner = { (!code_txt ~ ANY)* }
code_txt = { "'" }

text = ${ txt ~ text_inner ~ txt }
text_inner = { (!txt ~ ANY)* }
txt = _{ "'" }

strings = { (string ~ ",")* ~ string? }
string = { str ~ string_inner ~ str }
string_inner = @{ (!str ~ ANY)+ }
str = _{ "\"" }

int = @{ ("-" | "+")? ~ ASCII_DIGIT+ }

date = @{
    year ~ "/" ~ month ~ "/" ~ day
}
year = { "-"? ~ ASCII_DIGIT{4} }
month = { ASCII_DIGIT{2} }
day = { ASCII_DIGIT{2} }

WHITESPACE = _{ WHITE_SPACE }
