top = _{ SOI ~ (top_structs ~ ",")* ~ top_structs? ~ EOI }
top_structs = _{
    tags | props | text_item | emphasis | code | paragraph | heading | list | section | link | nav
}

tags = { "tags" ~ "{" ~ strings ~ "}" }

props = { "props" ~ "{" ~ props_inner ~ "}" }
props_inner = _{ (prop_tuple ~ ",")* ~ prop_tuple? ~ ","? }
prop_tuple = { ("(" ~ string ~ "," ~ prop_val ~ ")") }
prop_val = _{ string | text | date | int }

section = { "section" ~ "{" ~ heading ~ ("," ~ section_item)+ ~ ","? ~ "}" }
section_item = _{ paragraph | section | tags | props }

heading = { "head" ~ "{" ~ uint_capped ~ ("," ~ head_item)+ ~ ","? ~ "}" }
head_item = _{ string | emphasis | tags | props }

paragraph = { "par" ~ "{" ~ par_item ~ ("," ~ par_item)* ~ ","? ~ "}" }
par_item = _{ text_item | emphasis | code | list | link | tags | props }

emphasis = { "em" ~ "{" ~ em_st ~ "," ~ string ~ tp_postfix ~ "}" }
em_st = { "le" | "me" | "se" | "ld" | "md" | "sd" }

list = { "list" ~ "{" ~ list_type ~ ("," ~ par_item)+ ~ ","? ~ "}" }
list_type = { "dl" | "il" | "cl" }

nav = { "nav" ~ "{" ~ snav ~ ("," ~ snav)* ~ ","? ~ "}" }
snav = { "snav" ~ "{" ~ string ~ ("," ~ snav_item)+ ~ ","? ~ "}" }
snav_item = _{ link | snav | tags | props }

link = { "link" ~ "{" ~ string ~ ("," ~ link_item)+ ~ ","? ~ "}" }
link_item = _{ string | emphasis | tags | props }

code = { "code" ~ "{" ~ string ~ "," ~ string ~ "," ~ code_text ~ tp_postfix ~ "}" }
code_text = ${ code_txt ~ code_text_inner ~ code_txt }
code_text_inner = { (!code_txt ~ ANY)* }
code_txt = { "'" }

tp_postfix = _{ ("," ~ tags)? ~ ("," ~ props)? ~ ","? }

text_item = { text ~ text_meta? }
text = ${ txt ~ text_inner ~ txt }
text_meta = _{ "{" ~ ((tags ~ "," ~ props) | tags | props) ~ ","? ~ "}" }
text_inner = { (!txt ~ ANY)* }
txt = _{ "'" }

strings = { (string ~ ",")* ~ string? ~ ","? }
string = ${ str ~ string_inner ~ str }
string_inner = { (!str ~ ANY)+ }
str = _{ "\"" }

uint = { ASCII_DIGIT+ }
uint_capped = ${ ASCII_DIGIT{1, 19} }

int = @{ ("-" | "+")? ~ ASCII_DIGIT+ }

date = @{
    year ~ "/" ~ month ~ "/" ~ day
}
year = { "-"? ~ ASCII_DIGIT{1,4} }
month = { ASCII_DIGIT{1,2} }
day = { ASCII_DIGIT{1,2} }

WHITESPACE = _{ WHITE_SPACE }
COMMENT = _ { multi_line_comment | single_line_comment }
multi_line_comment = { "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
single_line_comment = { "//" ~ (!"\n" ~ ANY)* }
