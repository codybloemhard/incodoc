top = _{ SOI ~ (top_structs ~ ",")* ~ top_structs? ~ EOI }
top_structs = _{ tags | props | text_item | emphasis | code | paragraph | heading | list | section | link | nav }

tags = { "tags" ~ "{" ~ strings ~ "}" }

props = { "props" ~ "{" ~ props_inner ~ "}" }
props_inner = _{ (prop_tuple ~ ",")* ~ prop_tuple? }
prop_tuple = { ("(" ~ string ~ "," ~ prop_val ~ ")") }
prop_val = _{ string | text | date | int }

section = { "section" ~ "{" ~ heading ~ "," ~ (section_item ~ ",")* ~ section_item? ~ "}" }
section_item = _{ paragraph | section | tags | props }

heading = { "head" ~ "{" ~ uint_capped ~ "," ~ (head_item ~ ",")* ~ head_item? ~ "}" }
head_item = _{ text_item | emphasis | tags | props }

paragraph = { "par" ~ "{" ~ (par_item ~ ",")* ~ par_item? ~ "}" }
par_item = _{ text_item | emphasis | code | list | link | tags | props }

emphasis = { "em" ~ "{" ~ em_st ~ "," ~ text ~ "}" }
em_st = { "le" | "me" | "se" | "ld" | "md" | "sd" }

list = { "list" ~ "{" ~ list_type ~ "," ~ (list_item ~ ",")* ~ list_item? ~ "}" }
list_type = { "dl" | "il" | "cl" }
list_item = _{ text_item | emphasis | code | list | link | tags | props }

nav = { "nav" ~ "{" ~ (snav ~ ",")* ~ snav? ~ "}" }
snav = { "snav" ~ "{" ~ string ~ ("," ~ (link | snav))* ~ ("," ~ tags)? ~ ("," ~ props)? ~ "}" }

link = { "link" ~ "{" ~ (link_item ~ ",")+  ~ string ~ ("," ~ tags)? ~ ("," ~ props)? ~ "}" }
link_item = _{ text_item | emphasis }

code = { "code" ~ "{" ~ string ~ "," ~ string ~ "," ~ code_text ~ ("," ~ tags)? ~ ("," ~ props)? ~ "}" }
code_text = ${ code_txt ~ code_text_inner ~ code_txt }
code_text_inner = { (!code_txt ~ ANY)* }
code_txt = { "'" }

text_item = { text ~ text_meta? }
text = ${ txt ~ text_inner ~ txt }
text_meta = _{ "{" ~ ((tags ~ "," ~ props) | tags | props) ~ "}" }
text_inner = { (!txt ~ ANY)* }
txt = _{ "'" }

strings = { (string ~ ",")* ~ string? }
string = { str ~ string_inner ~ str }
string_inner = @{ (!str ~ ANY)+ }
str = _{ "\"" }

uint = { ASCII_DIGIT+ }
uint_capped = { ASCII_DIGIT{1, 19} }

int = @{ ("-" | "+")? ~ ASCII_DIGIT+ }

date = @{
    year ~ "/" ~ month ~ "/" ~ day
}
year = { "-"? ~ ASCII_DIGIT{4} }
month = { ASCII_DIGIT{2} }
day = { ASCII_DIGIT{2} }

WHITESPACE = _{ WHITE_SPACE }
